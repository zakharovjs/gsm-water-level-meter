;========================================================================================
;Измеритель температуры и расстояния с GSM-каналом
;Процедуры работы с ультразвуковым датчиком расстояния HC-SR04
;========================================================================================

;Подключение выводов датчика описано в основной программе.
;Для измерения использует таймер TMR1 и модуль CCP1 (подключение HCSR04_Echo строго к RB3/CCP1).
;Требует подключения модулей Arifm.inc (для деления) и MCUPauses.inc (для пауз).
;Использует переменные Tmp1 и DecEach20ms основной программы.
;Запрещает прерывания на время до 24 мкс.

;===== ПЕРЕМЕННЫЕ =======================================================================

		CBLOCK				;Переменные в банке 0
		  Dist_mksH,Dist_mksL:	1	  ;Измеренное расстояние [мкс]
		  Dist_smH,Dist_smL:	1	  ;Измеренное расстояние [см]
		ENDC

;Двухбайтовые переменные
#define		Dist_mks	Dist_mksH,Dist_mksL
#define		Dist_sm		Dist_smH,Dist_smL

;===== ПРОЦЕДУРЫ ========================================================================

;Измерение расстояния с помощью датчика HS-SR04 в Dist_mks [мкс] и Dist_sm [см].
;Значение результата: 0x0000 - датчик отсутствует; 0xFFFF - ошибка (скорее всего, максимальное расстояние).
;Длительность до 1600 мс. Портит Tmp1
GetDistance:	bsf	HCSR04_Vcc		;Подача питания датчика
		mov_lwf	.10,Tmp1		;Максимальное число попыток
_GD_Again:	call	GetDist_1Time		;Очередная попытка измерения
		ior_ffw	Dist_mks		;Dist_mks=0x0000 => Ошибка
		If_Z
		  goto	_GD_Error
		and_ffw	Dist_mks		;Dist_mks=0xFFFF => Ошибка
		incw
		If_Z
		  goto	_GD_Error
		cmp_lwf	.2,Dist_mksH		;Dist_mks<.512 => Ошибка
		If_2Lt1
		  goto	_GD_Error
		bcf	HCSR04_Vcc		;Отключение питания датчика
		movlw	0x34			;Результат корректный: Добавление кода события в лог и выход
		goto	Log_AddW			;(return там)
_GD_Error:	loop_f	Tmp1,_GD_Again		;Ошибка: Число повторений не исчерпано => Следующая попытка
		bcf	HCSR04_Vcc		;Отключение питания датчика
		;Формирование результата в случае ошибки
		ior_ffw	Dist_mks		;Dist_mks=0x0000 => Результат 0x0000
		If_Z
		  goto	_GD_ErrorEnd
		movlw2f	0xFFFF,Dist_mks		;Результат = 0xFFFF (в т.ч. для случая Dist_mks<.512)
		movlw2f	0xFFFF,Dist_sm
_GD_ErrorEnd:	movlw	0x35			;Результат некорректный: Добавление кода события в лог и выход
		goto	Log_AddW			;(return там)
;Примечание по результатам экспериментов:
;В большинстве случаев датчик возвращает корректный результат со второй попытки.
;С первой очень редко (даже если объект близко). Но бывает, что и с третьей, и с седьмой.

;===== СЛУЖЕБНЫЕ ПРОЦЕДУРЫ ==============================================================

;Попытка измерения расстояния с помощью датчика HS-SR04 в Dist_mks [мкс] и Dist_sm [см].
;Питание на датчик должно быть подано предварительно!
;Значение результата: 0x0000 - датчик отсутствует; 0xFFFF - ошибка, повторить измерение.
;Длительность до 160 мс
GetDist_1Time:	call	Pause100ms		;Пауза 80-100 мс для стабилизации питания и запуска датчика
		bcf	INTCON,GIE		;Запрет прерываний
		bsf	HCSR04_Trig		;Импульс начала измерения (min 10 мкс, беру 13 мкс)
		MCUPause_6mks
		MCUPause_6mks
		bcf	HCSR04_Trig
		mov_lwf	.3,DecEach20ms		;Запуск отсчёта времени ожидания (max 60 мс, чтобы не переполнялся TMR1)
		clrf	TMR1H			;Запуск TMR1 для отсчёта длительности импульса Echo
		clrf	TMR1L
		mov_lwf	0x01,T1CON			;Включён, Fosc/4, 1:1
		mov_lwf	0x05,CCP1CON		;Режим захвата переднего фронта RB3/CCP1 (HCSR04_Echo)
		bcf	PIR1,CCP1IF		;События не было
		bsf	INTCON,GIE		;Разрешение прерываний
		If_1	HCSR04_Echo		;Вход уже в высоком уровне (не может быть) => Ошибка
		  goto	_GD1_RaiseErr
_GD1_Wait1:	test_f	DecEach20ms		;Ожидание переднего фронта: Время ожидания истекло => Ошибка
		If_Z
		  goto	_GD1_RaiseErr
		If_0	PIR1,CCP1IF			;Событие не наступило => Ждём
		  goto	_GD1_Wait1
		bcf	INTCON,GIE		;Запрет прерываний
_GD1_Copy1:	mov_fwf	CCPR1L,Dist_mksL	;Сохранение значения времени события
		mov_fwf	CCPR1H,Dist_mksH
		cmp_fwf	CCPR1L,Dist_mksL		;Защита от изменения значения в процессе копирования
		If_NZ
		  goto	_GD1_Copy1
		clrf	CCP1CON			;Перед переключением режима CCP1 его рекомендуется выключать
		mov_lwf	0x04,CCP1CON		;Режим захвата заднего фронта RB3/CCP1 (HCSR04_Echo)
		bcf	PIR1,CCP1IF		;События не было
		bsf	INTCON,GIE		;Разрешение прерываний
		cmp_lwf	0x01,Dist_mksH		;Передний фронт не в пределах 256..511 мкс => Ошибка
		If_NZ
		  goto	_GD1_RaiseErr
_GD1_Wait0:	If_1	PIR1,CCP1IF		;Ожидание заднего фронта: Событие наступило => Обработка
		  goto	_GD1_Count
		test_f	DecEach20ms			;Время ожидания не истекло => Ждём
		If_NZ
		  goto	_GD1_Wait0
		movlw2f	0xFFFF,Dist_mks		;Результат = 0xFFFF
		movlw2f	0xFFFF,Dist_sm
		goto	_GD1_End
_GD1_Count:	clrf	CCP1CON			;Отключение модуля CCP для предотвращения изменения CCPR1 при вычитании
		sub2fwf	CCPR1H,CCPR1L,Dist_mks	;Расчёт разницы значений: Dist_mks:=Dist_mks-CCPR1 [мкс]
		neg2f	Dist_mks
		;Пересчёт расстояния из Dist_mks [мкс] в Dist_sm [см]
		mov2fwf	Dist_mks,PROD2		;Dist_sm:=Dist_mks/58
		mov_lwf	.58,MulArg
		call	Divide_2To1			;Деление PROD2/MulArg, остаток в DivOst отбрасываем
		mov2fwf	DivRes2,Dist_sm			;Копирование результата
_GD1_End:	;Завершение действий при любом сценарии
		clrf	CCP1CON			;Отключение модуля CCP
		clrf	T1CON			;Отключение TMR1
		return
_GD1_RaiseErr:	;Ошибка переднего фронта: считаем, что датчик отсутствует
		clr2f	Dist_mks		;Результат = 0x0000
		clr2f	Dist_sm			;Результат = 0x0000
		goto	_GD1_End
;Примечания по результатам экспериментов:
;1. Передний фронт HCSR04_Echo приходит примерно через 420 мкс от заднего фронта HCSR04_Trig.
;   Процедура проверяет, что он находится в диапазоне 256..511 мкс, иначе считается, что датчика нет.
;   При этом процедура возвращает значения 0x0000.
;2. Обратный фронт HCSR04_Echo иногда не приходит совсем. Процедура при этом возвращает значения 0xFFFF.
;   В таком случае следует повторять измерение.
;3. Минимальная длительность импульса HCSR04_Echo при нахождении объекта вплотную - 70 мкс.
;   Иногда проскакивают импульсы HCSR04_Echo порядка 310-410 мкс при отсутствии объекта в поле зрения.
;   Очевидно, датчик ловит собственное излучение не по воздуху, а по плате или как-то так.
;   Если длительность импульса менее примерно 512 мкс, следует повторять измерение.

;========================================================================================
